load("@aspect_rules_js//npm:defs.bzl", "npm_package")
load("@npm//:defs.bzl", "npm_link_all_packages")
load("@aspect_bazel_lib//lib:copy_to_bin.bzl", "copy_to_bin")
load("@npm//qwik/lib:vite/package_json.bzl", vite_bin = "bin")

npm_link_all_packages(name = "node_modules")

CONFIGS = [
    "vite.config.ts",
    "package.json",
    "tsconfig.json",
]

BUILD_DEPS = [":node_modules/" + d for d in [
    "vite",
    "@builder.io/qwik",
    "vite-tsconfig-paths",
]]

copy_to_bin(
    name = "vite_srcs_export",
    srcs = glob([
        "src/**/*.ts",
        "src/**/*.tsx",
    ]),
)

vite_bin.vite(
    name = "vite",
    srcs = BUILD_DEPS + CONFIGS + [":vite_srcs_export"],
    args = ["build --mode lib --config vite.config.ts"],
    chdir = package_name(),
    out_dirs = [
        "lib",
        "lib-types",
    ],
)

npm_package(
    name = "pkg",
    srcs = [
        "package.json",
        ":vite",
    ],
    visibility = ["//visibility:public"],
)
